# Maintainer: Haruue Icymoon <haruue@caoyue.com.cn>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-ct
pkgver=1.3.2+nginx_1.13.12
pkgrel=1

_ctver=1.3.2
_nginxver=1.13.12

pkgdesc='Nginx module that provides Certificate Transparency supports'
arch=('x86_64')
depends=("nginx=$_nginxver")
replaces=("nginx-ct-weak-ssl")
url="https://github.com/grahamedgecombe/nginx-ct"
license=('ISC')

source=(
	https://nginx.org/download/nginx-$_nginxver.tar.gz
        "nginx-ct-$_ctver.tar.gz"::"https://github.com/grahamedgecombe/nginx-ct/archive/v$_ctver.tar.gz"
)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
    echo "$_ctver+nginx_$_nginxver"
}

build() {
	cd "$srcdir"/nginx-$_nginxver
	./configure --with-compat --with-http_ssl_module --add-dynamic-module=../nginx-ct-$_ctver
	make modules
}

package() {
	install -Dm644 "$srcdir/"nginx-ct-$_ctver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$srcdir"/nginx-$_nginxver/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
