# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-ct
pkgver=1.3.2
pkgrel=2

_nginxver=1.13.8

pkgdesc='Nginx module that provides Certificate Transparency supports'
arch=('x86_64')
depends=("nginx=$_nginxver")
replaces=("nginx-ct-weak-ssl")
url="https://github.com/grahamedgecombe/nginx-ct"
license=('ISC')

source=(
	https://nginx.org/download/nginx-$_nginxver.tar.gz
        "nginx-ct-$pkgver.tar.gz"::"https://github.com/grahamedgecombe/nginx-ct/archive/v$pkgver.tar.gz"
)
sha256sums=('SKIP'
            'SKIP')

build() {
	cd "$srcdir"/nginx-$_nginxver
	./configure --with-compat --with-http_ssl_module --add-dynamic-module=../nginx-ct-$pkgver
	make modules
}

package() {
	install -Dm644 "$srcdir/"nginx-ct-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$srcdir"/nginx-$_nginxver/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
